%import common.SIGNED_NUMBER
%import common.CNAME

%import common.WS
%ignore WS

number : SIGNED_NUMBER
bool : /(true|false)/
ident : CNAME
!measure : delta "->" delta

!prog : def 
      | def ";" prog

!def : funcdef | measuredef
!funcdef : "let" "rec"? ident ":" type "=" expr
!measuredef : "measure" ident ":" delta "->" delta "=" espec

!delta : "()" | "int" | "bool"
       | delta "*" delta
       | delta "list"
       | delta "array"

!type : delta
     | "{" ident ":" delta "|" espec "}"
     | "(" ident ":" type ")" "->" type "@" cspec
     | delta "->" delta

!cspec : espec
      | "O(" espec ")"

!espec : ident | number | bool
       | "not" espec | espec opspec espec
       | "log" espec
       | "forall" ident+ "." espec
       | "exists" ident+ "." espec
       | measure espec
       | "if" espec "then" espec "else" espec
       | "(" espec ")"

!opspec : "+" | "-" | "*" | "/" | "mod" | "^"
       | "==" | "<>" | "<" | ">" | "<=" | ">="
       | "&&" | "||"

!expr : ident | number | bool
      | "not" expr | expr7
      | "if" expr "then" expr "else" expr
      | "let" "rec"? ident ":" type "=" expr "in" expr
      | "fun" "(" ident ":" type ")" "->" expr
      | expr expr
      | "[]" | expr "::" expr
      | "match" expr "with" clauses
      | "(" expr ")"

!?expr0 : expr0 expr
        | expr

!?expr1 : expr0 "^" expr1
        | expr0

!?expr2 : expr2 "*" expr1
        | expr2 "/" expr1
        | expr2 "mod" expr1
        | expr1

!?expr3 : expr3 "+" expr2
        | expr3 "-" expr2
        | expr2

!?expr4 : expr3 "::" expr4
        | expr3

!?expr5 : expr5 "=" expr4
        | expr5 "<>" expr4
        | expr5 "<" expr4
        | expr5 ">" expr4
        | expr5 "<=" expr4
        | expr5 ">=" expr4
        | expr4

!?expr6 : expr6 "&&" expr5
        | expr5

!?expr7 : expr7 "||" expr6
        | expr6

!clauses : pat "->" expr ("|" clauses)?

!pat : ident | "_" | number | bool
     | "[]" | pat "::" pat
     | "(" pat ")" | "(" pat "," pat ")"

!op : "+" | "-" | "*" | "/" | "mod"
   | "==" | "<>" | "<" | ">" | "<=" | ">="
   | "&&" | "||"
