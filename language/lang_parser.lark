%import common.SIGNED_NUMBER
%import common.CNAME

%import common.WS
%ignore WS

number : SIGNED_NUMBER
bool : /(true|false)/
ident : CNAME
idents : ident+

!prog : defn
      | defn ";" prog

!defn : funcdef | measuredef
!funcdef : "let" "rec"? ident ":" type "=" expr
!measuredef : "measure" ident "(" ident ":" delta ")" ":" delta "=" espec

?delta : delta1
!delta_init : "()" | "int" | "bool" | "(" delta ")"
!?delta0 : delta0 "list" | delta0 "array" | delta_init
!?delta1 : delta0 ("*" delta0)*

!type : delta
      | "{" ident ":" delta "|" espec "}"
      | "(" ident ":" type ")" "->" type "@" cspec
      | delta "->" delta

!cspec : "O(" espec ")"
       | espec

?espec : espec9

!espec_init : ident | number | bool
            | "(" espec ")"

!?espec0 : espec0 espec_init
         | "not" espec_init
         | "log" espec_init
         | espec_init

!?espec1 : espec0 "^" espec1
         | espec0

!?espec2 : espec2 "*" espec1
         | espec2 "/" espec1
         | espec2 "mod" espec1
         | espec1

!?espec3 : espec3 "+" espec2
         | espec3 "-" espec2
         | espec2

!?espec4 : espec3

!?espec5 : espec5 "=" espec4
         | espec5 "<>" espec4
         | espec5 "<" espec4
         | espec5 ">" espec4
         | espec5 "<=" espec4
         | espec5 ">=" espec4
         | espec4

!?espec6 : espec6 "&&" espec5
         | espec5

!?espec7 : espec7 "||" espec6
         | espec6

!?espec8 : "forall" idents "." espec8
         | "exists" idents "." espec8
         | espec7

!?espec9 : "if" espec9 "then" espec9 "else" espec9
         | espec8

?expr : expr8

!expr_init : ident | number | bool | "[]"
            | "(" expr ")"

!?expr0 : expr0 expr_init
        | "not" expr_init
        | expr_init

!?expr1 : expr0
// expr0 "^" expr1

!?expr2 : expr2 "*" expr1
        | expr2 "/" expr1
        | expr2 "mod" expr1
        | expr1

!?expr3 : expr3 "+" expr2
        | expr3 "-" expr2
        | expr2

!?expr4 : expr3 "::" expr4
        | expr3

!?expr5 : expr5 "=" expr4
        | expr5 "<>" expr4
        | expr5 "<" expr4
        | expr5 ">" expr4
        | expr5 "<=" expr4
        | expr5 ">=" expr4
        | expr4

!?expr6 : expr6 "&&" expr5
        | expr5

!?expr7 : expr7 "||" expr6
        | expr6

!?expr8 : "if" expr8 "then" expr8 "else" expr8
       | "let" "rec"? ident ":" type "=" expr8 "in" expr8
       | "fun" "(" ident ":" type ")" "->" expr8
       | "match" expr8 "with" clauses
       | expr7

!clauses : pat "->" expr ("|" clauses)?

!pat : ident | "_" | number | bool
     | "[]" | pat "::" pat
     | "(" pat ")" | "(" pat "," pat ")"
